<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   showStatusBar="false"
					   width="1024"
					   height="768"
					   applicationComplete="_handleAppComplete(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			//--------------------------------------------------------------------------
			//
			//  Properties
			//
			//--------------------------------------------------------------------------
			
			//----------------------------------
			//  Private:
			//----------------------------------
			
			private var animations:Array;
			private var charList:XMLList;
			private var fileStream:FileStream
			private var file:File;
			private var frames:Array;
			private var urlToFile:String;
			private var fntXML:XML;
			
			//--------------------------------------------------------------------------
			//
			//  Methods
			//
			//--------------------------------------------------------------------------
			
			//----------------------------------
			//  Private:
			//----------------------------------
			
			private function _handleAppComplete(event:FlexEvent):void
			{
				_init();
			}
			
			private function _init():void
			{
				createFileSystem();
			}
			
			private function createFileSystem():void
			{
				file = new File();
				file.addEventListener(Event.CANCEL, handleCancel, false, 0, true);
				file.addEventListener(Event.SELECT, handleSelect, false, 0, true);
			}
			
			private function generateOutput():void
			{
				var curFrame:Object;
				
				frames = [];
				animations = [];
				
				var len:uint = charList.length();
				for(var i:int=0; i<len; i++) 
				{
					if(charList[i].@id == 32)
					{
						continue;
					}
					curFrame = {};
					curFrame.x = charList[i].@x;
					curFrame.y = charList[i].@y;
					curFrame.width = charList[i].@width;
					curFrame.height = charList[i].@height;
					frames.push(curFrame);
					animations.push(String.fromCharCode(charList[i].@id));
				}
				output.text = "{\n\n";
				output.text += "\t\"images\": [\n";
				output.text += "\t\t\""+fntXML.pages[0].page[0].@file+"\"\n";
				output.text += "\t],\n\n";
				output.text += "\t\"frames\": [\n";
				len = frames.length;
				for(i=0; i<len; i++) 
				{
					if(i < len-1)
					{
						output.text += "\t\t["+frames[i].x+", "+frames[i].y+", "+frames[i].width+", "+frames[i].height+"],\n";
					}
					else
					{
						output.text += "\t\t["+frames[i].x+", "+frames[i].y+", "+frames[i].width+", "+frames[i].height+"]\n";
					}
				}
				output.text += "\t],\n\n";
				output.text += "\t\"animations\": {\n";
				len = animations.length;
				for(i=0; i<len; i++) 
				{
					if(i < len-1)
					{
						output.text += "\t\t\""+animations[i]+"\":["+i+"],\n";
					}
					else
					{
						output.text += "\t\t\""+animations[i]+"\":["+i+"]\n";
					}
				}
				output.text += "\t}\n\n";
				output.text += "}";
			}
			
			private function handleCancel(event:Event):void
			{
				
			}
			
			private function handleClick(event:MouseEvent):void
			{
				var txtFilter:FileFilter = new FileFilter("Text", "*.fnt");
				switch(event.currentTarget)
				{
					case browseBtn:
						file.browseForOpen("Select the fnt file that you wish to convert.", [txtFilter]);
						break;
					case convertBtn:
						loadFntFile();
						break;
				}
			}
			
			private function handleSelect(event:Event):void
			{
				urlToFile = event.currentTarget.url;
				targetFile.text = event.currentTarget.nativePath;
			}
			
			private function loadFntFile():void
			{
				fileStream = new FileStream();
				fileStream.open(file, FileMode.READ);
				fntXML = XML(fileStream.readUTFBytes(fileStream.bytesAvailable));
				fileStream.close();
				
				charList = fntXML..char;
				generateOutput();
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup paddingBottom="12" paddingLeft="12" paddingRight="12" horizontalAlign="left" verticalAlign="top" width="100%" height="100%">
		<s:Label fontSize="32" text="Fnt to Json Converter" paddingTop="12" textAlign="center" width="100%" />
		<s:Label fontSize="14" text="A tool used to convert standard bitmap text .fnt files to .json files for use with CreateJS" paddingTop="12" textAlign="center" width="100%" />
		<s:Spacer height="10" />
		<s:HGroup verticalAlign="middle" width="100%">
			<s:Label fontSize="18" text="Font file to convert:" />
			<s:TextInput id="targetFile" editable="false" width="100%" />
			<s:Button id="browseBtn" click="handleClick(event)" label="Browse" />
		</s:HGroup>
		<s:Spacer height="10" />
		<s:HGroup horizontalAlign="center" width="100%">
			<s:Button id="convertBtn" click="handleClick(event)" label="Convert" />
		</s:HGroup>
		<s:Spacer height="10" />
		<s:Label fontSize="18" text="Output:" />
		<s:TextArea id="output" width="100%" height="100%" />
	</s:VGroup>
</s:WindowedApplication>
